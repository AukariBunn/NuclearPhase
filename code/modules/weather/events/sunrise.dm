/datum/weather_event/sunrise
	override_all = TRUE

/datum/weather_event/sunrise/proc/interpolate_temperature(rand_min, rand_max, sleep_time, target)
	set waitfor = 0
	while(using_map.exterior_atmosphere.temperature < target)
		using_map.exterior_atmosphere.temperature += rand(rand_min, rand_max)
		sleep(sleep_time)

/datum/weather_event/sunrise/start()
	..()
	var/obj/abstract/weather_system/weather = using_map.weather_system
	weather.weather_system.set_state(/decl/state/weather/sunriseone)
	weather.icon_state = "snowfall_light"
	using_map.exterior_atmosphere.temperature = 26 //initial flash
	interpolate_temperature(1, 1.5, 10, 48) //hydrogen melting
	var/list/nonsurface_mobs = human_mob_list
	for(var/turf/T in surface_turfs)
		T.set_ambient_light(COLOR_SUNRISE_SURFACE1, 1)
	for(var/area/A in surface_areas)
		A.do_ambience = FALSE
	for(var/mob/living/carbon/human/H in surface_mobs)
		nonsurface_mobs -= H
		H.lastarea.clear_ambience(H)
		sound_to(H, sound('sound/music/sunrise.ogg',0,50,sound_channels.lobby_channel))
		to_chat(H, "<span class=bigdanger>The sky fiercely erupts bright crimson-red in a shower of shattered ice fragments, like a hail you've never experienced before. Slow-moving streams of raging flames and smoking clouds rush away from the approaching, scorching, death-bringing rising star. These melting sky shields won't last for long, the never ending doom is soon to come.</span>")
		H.add_client_color(/datum/client_color/sunrise1, 20, CUBIC_EASING)
	spawn(rand(30, 100))
		weather.lightning_strike()
	weather.favorable_wind_speed = 70
	weather.wind_speed = 70
	weather.handle_wind()
	for(var/mob/living/carbon/human/H in nonsurface_mobs)
		if(H.job == "Office Clerk")
			nonsurface_mobs -= H
			continue
		to_chat(H, SPAN_ERPBOLD("You feel strong vibrations emanating from the surface, the deathbringer star is rising once again..."))
	SSmoods.call_mood_event("sunrise", list("sunrise"), nonsurface_mobs)

	sleep(300)
	interpolate_temperature(3, 5, 10, 107) //methane melting
	weather.icon_state = "hail"
	weather.favorable_wind_speed = 185
	weather.wind_speed = 185
	weather.handle_wind()
	for(var/mob/living/carbon/human/H in surface_mobs)
		to_chat(H, "<span class=bigdanger>Spiteful winds pick up and howl all around you, wheezing like thousands of blowing whistles. The temperature starts to sharply pick up as your footing gets wet, it gets harder and harder to move forward through intermixed layers of frozen solid gas. The ground rumbles and cracks, forming beautiful patterns that almost overwhelm your preservation instincts.</span>")
		H.add_client_color(/datum/client_color/sunrise2, 20, SINE_EASING)
		H.remove_client_color(/datum/client_color/sunrise1)
	for(var/turf/exterior/surface/T in surface_turfs)
		T.set_ambient_light(COLOR_SUNRISE_SURFACE2, 2)
		T.switch_cracks(FALSE)

	sleep(290)
	interpolate_temperature(13, 17, 10, 402) //water melting
	weather.icon_state = "storm"
	weather.favorable_wind_speed = 330
	weather.wind_speed = 330
	weather.handle_wind()
	for(var/mob/living/carbon/human/H in surface_mobs)
		to_chat(H, "<span class=bigdanger>Small shockwaves form on your suit as the wind goes transonic, a great sign of what's to come. You're in a giant wind tunnel, like a lab animal clinging for the last chance to get out and survive for a little longer. There is no time.</span>")
		H.add_client_color(/datum/client_color/sunrise3, 20, SINE_EASING)
		H.remove_client_color(/datum/client_color/sunrise2)
	for(var/turf/T in surface_turfs)
		T.set_ambient_light(COLOR_SUNRISE_SURFACE3, 2)
		T.footstep_type = /decl/footsteps/water
	sleep(240)
	var/image/fire_overlay = image(icon = 'icons/effects/fire.dmi', icon_state = "1", layer = FIRE_LAYER)
	fire_overlay.mouse_opacity = 0
	fire_overlay.blend_mode = BLEND_ADD
	fire_overlay.color = FIRE_COLOR_DEFAULT
	fire_overlay.alpha = 140
	interpolate_temperature(20, 30, 10, 574) //flash
	weather.icon_state = "ashfall_light"
	weather.favorable_wind_speed = 380
	weather.wind_speed = 380
	weather.handle_wind()
	for(var/mob/living/carbon/human/H in surface_mobs)
		to_chat(H, "<span class=bigdanger>An extremely strong and loud sonic shockwave knocks you down to the ground that slowly sinks down, evaporating entirely to the point of leaving only barren rock behind. The climate system of your suit barely manages to control sharp unexpected pressure waves generated by such a sudden flashing of ice into gas.</span>")
		H.flash_eyes(99)
		H.add_client_color(/datum/client_color/sunrise4, 10, CUBIC_EASING)
		H.remove_client_color(/datum/client_color/sunrise3)
	for(var/turf/exterior/surface/T in surface_turfs)
		T.overlays += fire_overlay
		T.set_ambient_light(COLOR_SUNRISE_SURFACE4, 3)
		T.icon = 'icons/turf/exterior/volcanic.dmi'
		T.possible_states = 0
		T.icon_state = "[rand(0, T.possible_states)]"
		T.dirt_color = COLOR_GRAY20
		T.icon_edge_layer = EXT_EDGE_VOLCANIC
		T.update_icon()
		T.footstep_type = /decl/footsteps/asteroid
		T.switch_cracks(TRUE)

	sleep(460)
	interpolate_temperature(2, 5, 10, 613)
	weather.favorable_wind_speed = 320
	weather.wind_speed = 320
	weather.handle_wind()
	for(var/mob/living/carbon/human/H in surface_mobs)
		to_chat(H, "<span class=bigdanger>The flaming light suddenly calms down, followed by a bright flash of upper atmosphere's fiery ignition. Your final judgement is yet again delayed for a few dozen seconds, right in preparation for a final strike of what became the end of many...</span>")
		H.remove_client_color(/datum/client_color/sunrise4)
	spawn(0) //So we won't slow down
		for(var/turf/T in surface_turfs)
			T.overlays -= fire_overlay
			T.set_ambient_light(COLOR_SUNRISE_SURFACE3, 2)
			if(prob(1))
				addtimer(CALLBACK(GLOBAL_PROC, .proc/playsound, T, 'sound/effects/explosionfar.ogg', 200, 1, 50, 1), rand(1, 50))

	sleep(260)
	for(var/mob/living/carbon/human/H in surface_mobs)
		to_chat(H, "<span class=bigdanger>Just moments away from a terrifying demise, the repercussion of the dangerous adventure you've embarked on, comes the final realization; your fate is only marginally better from what others shall experience, most will smile right before their instantaneous death in a violent flashover, knowing that their story of suffering has finally come to an end. And so will you?</span>")

	sleep(200)
	using_map.exterior_atmosphere.temperature = 970
	weather.favorable_wind_speed = 750
	weather.wind_speed = 750
	weather.handle_wind()
	for(var/mob/living/carbon/human/H in surface_mobs)
		to_chat(H, "<span class=bigdanger>In a nick of a second, your body gets torn into millions of pieces by immense shockwaves generated in a one large flash of exploded gas. No one will remember you.</span>")
		H.flash_eyes(99)
		H.dust()
	for(var/turf/T in surface_turfs)
		T.set_ambient_light(COLOR_HOT_SURFACE, 2)
	weather.weather_system.set_state(/decl/state/weather/ash)
	for(var/area/surface/A in surface_areas)
		A.do_ambience = TRUE
		A.switch_phases(1)

	sleep(100)
	weather.favorable_wind_speed = 210
	weather.wind_speed = 210
	weather.handle_wind()
	end()